/*
 * Copyright (C) 2022-2025 Hedera Hashgraph, LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.graphql_java_generator.plugin.conf.CustomScalarDefinition
import com.graphql_java_generator.plugin.conf.PluginMode

description = "Hedera Mirror Node GraphQL"

plugins {
    id("com.graphql-java-generator.graphql-gradle-plugin3")
    id("spring-conventions")
}

dependencies {
    annotationProcessor("org.mapstruct:mapstruct-processor")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    compileOnly("com.graphql-java-generator:graphql-java-client-runtime") {
        exclude(group = "org.springframework.security")
    }
    implementation(project(":common"))
    implementation(platform("org.springframework.cloud:spring-cloud-dependencies"))
    implementation("com.graphql-java:graphql-java-extended-scalars")
    implementation("com.graphql-java:graphql-java-extended-validation")
    implementation("io.github.mweirauch:micrometer-jvm-extras")
    implementation("jakarta.inject:jakarta.inject-api")
    implementation("org.mapstruct:mapstruct")
    implementation("org.springframework.boot:spring-boot-actuator-autoconfigure")
    implementation("org.springframework.boot:spring-boot-starter-graphql")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.cloud:spring-cloud-starter-bootstrap")
    implementation("org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8-config")
    runtimeOnly("io.micrometer:micrometer-registry-prometheus")
    runtimeOnly(
        group = "io.netty",
        name = "netty-resolver-dns-native-macos",
        classifier = "osx-aarch_64",
    )
    runtimeOnly("org.postgresql:postgresql")
    testImplementation(project(path = ":common", configuration = "testClasses"))
    testImplementation("io.projectreactor:reactor-test")
    testImplementation("org.flywaydb:flyway-database-postgresql")
    testImplementation("org.springframework.boot:spring-boot-testcontainers")
    testImplementation("org.springframework.graphql:spring-graphql-test")
    testImplementation("org.testcontainers:postgresql")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

generatePojoConf {
    isAddRelayConnections = false
    isCopyRuntimeSources = true
    isUseJakartaEE9 = true
    javaTypeForIDType = "java.lang.String"
    mode = PluginMode.server
    packageName = "com.hedera.mirror.graphql.viewmodel"
    schemaFilePattern = "**/*.graphqls"
    setCustomScalars(
        arrayOf(
            CustomScalarDefinition(
                "Duration",
                "java.time.Duration",
                "",
                "com.hedera.mirror.graphql.config.GraphQlDuration.INSTANCE",
                "",
            ),
            CustomScalarDefinition("Long", "java.lang.Long", "", "graphql.scalars.GraphQLLong", ""),
            CustomScalarDefinition("Object", "java.lang.Object", "", "graphql.scalars.Object", ""),
            CustomScalarDefinition(
                "Timestamp",
                "java.time.Instant",
                "",
                "com.hedera.mirror.graphql.config.GraphQlTimestamp.INSTANCE",
                "",
            ),
        )
    )
}

tasks.withType<JavaCompile>().configureEach {
    dependsOn(tasks.generatePojo)
    if (name == "compileJava") {
        options.compilerArgs.addAll(
            listOf(
                "-Amapstruct.defaultComponentModel=jakarta",
                "-Amapstruct.defaultInjectionStrategy=constructor",
                "-Amapstruct.disableBuilders=true",
                "-Amapstruct.unmappedTargetPolicy=IGNORE", // Remove once all Account fields have
                // been mapped
            )
        )
    }
}

java.sourceSets["main"].java { srcDir(tasks.generatePojo) }
